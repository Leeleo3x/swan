plugins {
    id "com.cisco.external-build" version "1.11"
}

import com.cisco.gradle.externalbuild.ExternalNativeLibrarySpec
import com.cisco.gradle.externalbuild.tasks.CMake

def getIntermediateDir = { binary ->
    file("${buildDir}/external-build/${binary.component.name}/${binary.targetPlatform.name}")
}

def getInstallDir = { binary ->
    file("${buildDir}/install/${binary.component.name}/${binary.targetPlatform.name}")
}

project(":swift-wala-translator") {
    model {
        components {
            swiftWala(ExternalNativeLibrarySpec) {
                buildConfig(CMake) {
                    def intDir = getIntermediateDir(binary)
                    def outputDir = getInstallDir(binary)

                    redirectOutput = false

                    cmakeRoot '.'
                    cmakeArgs "-DCMAKE_INSTALL_PREFIX=${outputDir}",
                            "-DCMAKE_C_COMPILER=clang",
                            "-DCMAKE_CXX_COMPILER=clang++",
                            "-DWALA_PATH_TO_SWIFT_BUILD=${WALA_PATH_TO_SWIFT_BUILD}",
                            "-DSWIFT_PATH_TO_LLVM_SOURCE=${SWIFT_PATH_TO_LLVM_SOURCE}",
                            "-DSWIFT_PATH_TO_LLVM_BUILD=${SWIFT_PATH_TO_LLVM_BUILD}",
                            "-DSWIFT_PATH_TO_CMARK_BUILD=${SWIFT_PATH_TO_CMARK_BUILD}",
                            "-DWALA_DIR=${WALA_DIR}"
                    targets 'all', 'install'
                    inputs.dir 'lib'
                    inputs.file 'CMakeLists.txt'
                    workingDir intDir
                }

                buildOutput {
                    def outputDir = getInstallDir(binary)
                    outputFile = file("${outputDir}/lib/libswiftWala.so")
                    exportedHeaders {
                      srcDir "${projectDir}/include"
                    }
                }
            }

        }
    }

}
